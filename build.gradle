buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:2.2.4'
  }
}

apply plugin: 'native-component'
apply plugin: 'maven-publish'
apply plugin: 'artifactory-publish'

artifactory {
  contextUrl = "http://oss.jfrog.org"

  resolve {
    repository {
      repoKey = 'libs-releases'
    }
  }

  publish {
    repository {
      repoKey = 'oss-snapshot-local'
      username = System.env.BINTRAY_USER
      password = System.env.BINTRAY_API_KEY
    }

    defaults {
      publications 'linux-x86_64'
    }
  }
}

model {
  platforms {
    create("linux-x86_64") {
      operatingSystem "linux"
      architecture "x86_64"
    }
  }
}

publishing {
  publications {

    platforms.each { platform ->
      create(platform.name, MavenPublication) {
        def nativeBuildDir = "${buildDir}/${platform.operatingSystem.name}-${platform.architecture.name}"

        artifactId = "${project.name}-${platform.name}"

        artifact("${nativeBuildDir}/avian") {
          extension "bin"
        }

        artifact("${nativeBuildDir}/classpath.jar") {
          classifier "classpath"
        }

        artifact("${nativeBuildDir}/binaryToObject/binaryToObject") {
          classifier "binaryToObject"
          extension "bin"
        }

        artifact("${nativeBuildDir}/libavian.a") {
          classifier "libavian"
          extension "a"
        }
      }
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.0'
}